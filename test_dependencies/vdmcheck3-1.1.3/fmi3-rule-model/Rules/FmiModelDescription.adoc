// This adds the "functions" section header for VDM only
ifdef::hidden[]
// {vdm}
functions
// {vdm}
endif::[]

==== A.{section}.{counter:subsection} FmiModelDescription Rules
:!typerule:
===== A.{section}.{subsection}.{counter:typerule} Effective Model Variables
Note that  ModelVariables are passed to some of the rules in this section, in addition to the FmiModelDescription. The ModelVariables passed are _effective ModuleVariables_, whose fields have been set to default values or inherited from the _declaredType_ of the variable. The calculation of effective ModelVariables is defined by a function called _effectiveVariables_.

===== A.{section}.{subsection}.{counter:typerule} Rule: validModelTypes
[[validModelTypes]]
ifdef::hidden[]
// {vdm}

-- @DocLink("<FMI3_STANDARD>#table-schema-fmiModelDescription")
// {vdm}
endif::[]
// {vdm}
----
validModelTypes: FmiModelDescription +> bool
validModelTypes(fmd) ==
	-- @OnFail("%NAME: ModelExchange, CoSimulation or ScheduledExecution must be set")
	( fmd.modelExchange <> nil or fmd.coSimulation <> nil or fmd.scheduledExecution <> nil );
----
// {vdm}
See <<table-schema-fmiModelDescription>>.

===== A.{section}.{subsection}.{counter:typerule} Rule: validGenerationDateAndTime
[[validGenerationDateAndTime]]
ifdef::hidden[]
// {vdm}

-- @DocLink("<FMI3_STANDARD>#table-schema-fmiModelDescription-attributes")
// {vdm}
endif::[]
// {vdm}
----
validGenerationDateAndTime: FmiModelDescription +> bool
validGenerationDateAndTime(fmd) ==
	-- @OnFail("%NAME: generationDateAndTime should be YYYY-MM-DDThh:mm:ssZ at %#s",
	-- loc2str(fmd.location))
	( fmd.generationDateAndTime <> nil => iso8601(fmd.generationDateAndTime) );
----
// {vdm}
See <<table-schema-fmiModelDescription-attributes>>.
	
===== A.{section}.{subsection}.{counter:typerule} Rule: validTypeUnits
[[validTypeUnits]]
ifdef::hidden[]
// {vdm}

-- @DocLink("<FMI3_STANDARD>#table-type-details")
// {vdm}
endif::[]
// {vdm}
----
validTypeUnits: FmiModelDescription +> bool
validTypeUnits(fmd) ==
	fmd.typeDefinitions <> nil => allOf
	([
		tdef.unit <> nil =>
			-- @OnFail("%NAME: Type %s, unit %s not defined in UnitDefinitions at %#s",
			-- tdef.name, tdef.unit, loc2str(tdef.location))
			( fmd.unitDefinitions <> nil
				and exists u in seq fmd.unitDefinitions & u.name = tdef.unit )

		| tdef in seq fmd.typeDefinitions & isFloat(tdef)
	]);
----
// {vdm}
See <<table-type-details>>.

===== A.{section}.{subsection}.{counter:typerule} Rule: validVariableTypes
[[validVariableTypes]]
ifdef::hidden[]
// {vdm}

-- @DocLink("<FMI3_STANDARD>#ModelVariables", "<FMI3_STANDARD>#definition-of-types")
// {vdm}
endif::[]
// {vdm}
----
validVariableTypes: FmiModelDescription +> bool
validVariableTypes(fmd) == allOf
	([
		mv.declaredType <> nil =>
		let tdef = lookupType(mv.declaredType, fmd.typeDefinitions) in
			-- @OnFail("%NAME: %s type %s not found at %#s",
			-- mv.name, mv.declaredType, loc2str(mv.location))
			( tdef <> nil )

			-- @OnFail("%NAME: %s type %s mismatch at %#s",
			-- mv.name, mv.declaredType, loc2str(mv.location))
			and ( kindOf(tdef) = kindOf(mv) )

		| mv in seq fmd.modelVariables
	]);
----
// {vdm}
- See <<ModelVariables>>.
- See <<definition-of-types>>.

===== A.{section}.{subsection}.{counter:typerule} Rule: validTypeNames
[[validTypeNames]]
ifdef::hidden[]
// {vdm}

-- @DocLink("<FMI3_STANDARD>#table-type-details")
// {vdm}
endif::[]
// {vdm}
----
validTypeNames: FmiModelDescription +> bool
validTypeNames(fmd) == fmd.typeDefinitions <> nil => allOf
	([
		-- @OnFail("%NAME: TypeDefinition and Variable names overlap: %s at %#s",
		-- tdef.name, loc2str(tdef.location))
		( not exists mv in seq fmd.modelVariables & mv.name = tdef.name )

		| tdef in seq fmd.typeDefinitions
	]);
----
// {vdm}
See <<table-type-details>>.

===== A.{section}.{subsection}.{counter:typerule} Rule: validVariableNames
[[validVariableNames]]
ifdef::hidden[]
// {vdm}

-- @DocLink("<FMI3_STANDARD>#ModelVariables")
// {vdm}
endif::[]
// {vdm}
----
validVariableNames: FmiModelDescription +> bool
validVariableNames(fmd) ==
	fmd.variableNamingConvention = <structured> => allOf(conc
	[
		[
			-- @OnFail("%NAME: Structured name %s invalid at %#s", mv.name, loc2str(mv.location))
			( validStructuredName(mv.name) ),

			-- @OnFail("%NAME: Name %s is not Float at %#s", mv.name, loc2str(mv.location))
			( mv.name(1, ..., 4) = "der(" => isFloat(mv) )
		]

		| mv in seq fmd.modelVariables
	]);
----
// {vdm}
See <<ModelVariables>>.

===== A.{section}.{subsection}.{counter:typerule} Rule: validStart
[[validStart]]
ifdef::hidden[]
// {vdm}

-- @DocLink("<FMI3_STANDARD>#table-type-attributes")
// {vdm}
endif::[]
// {vdm}
----
validStart: FmiModelDescription * ModelVariables +> bool
validStart(-, evs) == allOf
	([
		[
			ev.start <> nil =>
				-- @OnFail("%NAME: all start values must be between %s and %s at %#s",
				-- ev.min, ev.max, ev.location)
				( forall sv in seq ev.start & ev.min <= sv and ev.max >= sv ),

			ev.start = nil and ev.initial <> <calculated> =>
				-- @OnFail("%NAME: Warning: implicit start of 0 not within min/max at %#s",
				-- ev.location)
				( (ev.min <> nil => ev.min <= 0) and
				  (ev.max <> nil => ev.max >= 0) ) or true	-- NOTE warning
		]

		| ev in seq evs & is_NumericKind(kindOf(ev))
	]);
----
// {vdm}
See <<table-type-attributes>>.

===== A.{section}.{subsection}.{counter:typerule} Rule: validMinMax
[[validMinMax]]
ifdef::hidden[]
// {vdm}

-- @DocLink("<FMI3_STANDARD>#table-type-attributes")
// {vdm}
endif::[]
// {vdm}
----
validMinMax: FmiModelDescription * ModelVariables +> bool
validMinMax(-, evs) == allOf
	([
		let mk_(kmin, kmax) = minMaxOfKind(kindOf(ev)) in
		[
			-- @OnFail("%NAME: max %s is not a valid value of this type", ev.max)
			( ev.max <= kmax and ev.max >= kmin ),

			-- @OnFail("%NAME: min %s is not a valid value of this type", ev.min)
			( ev.min <= kmax and ev.min >= kmin ),

			-- @OnFail("%NAME: max %s not >= min %s", ev.max, ev.min)
			( ev.max >= ev.min )
		]

		| ev in seq evs & is_NumericKind(kindOf(ev))
	])
pre forall ev in seq evs &
	is_NumericKind(kindOf(ev)) => ev.min <> nil and ev.max <> nil;
----
// {vdm}
See <<table-type-attributes>>.

===== A.{section}.{subsection}.{counter:typerule} Rule: validMultipleSets
[[validMultipleSets]]
ifdef::hidden[]
// {vdm}

-- @DocLink("<FMI3_STANDARD>#table-variableBase-attributes")
// {vdm}
endif::[]
// {vdm}
----
validMultipleSets: FmiModelDescription * ModelVariables +> bool
validMultipleSets(fmd, evs) == allOf
	([
		ev.canHandleMultipleSetPerTimeInstant = true =>
			-- @OnFail("%NAME: Variable %s, canHandleMultipleSetPerTimeInstant invalid at %#s",
			-- ev.name, loc2str(ev.location))
			( fmd.modelExchange <> nil and ev.causality = <input> )

		| ev in seq evs
	]);
----
// {vdm}
See <<table-variableBase-attributes>>.

===== A.{section}.{subsection}.{counter:typerule} Rule: validReinits
[[validReinits]]
ifdef::hidden[]
// {vdm}

-- @DocLink("<FMI3_STANDARD>#ModelVariables")
// {vdm}
endif::[]
// {vdm}
----
validReinits: FmiModelDescription +> bool
validReinits(fmd) == allOf
	([
		isFloat(mv) and mv.reinit <> nil =>
			-- @OnFail("%NAME: %s, Float reinit for model exchange continuous time only at %#s",
			-- mv.name, loc2str(mv.location))
			( isContinuousTimeState(mv, fmd.modelVariables) and fmd.modelExchange <> nil )

		| mv in seq fmd.modelVariables
	]);
----
// {vdm}
See <<ModelVariables>>.

===== A.{section}.{subsection}.{counter:typerule} Rule: validCSEventMode
[[validCSEventMode]]
ifdef::hidden[]
// {vdm}

-- @DocLink("<FMI3_STANDARD>#table-CoSimulation-details")
// {vdm}
endif::[]
// {vdm}
----
validCSEventMode: FmiModelDescription +> bool
validCSEventMode(fmd) ==
	fmd.coSimulation <> nil =>
		-- @OnFail("%NAME: hasEventMode must be true since FMU has clocks at %#s",
		-- loc2str(fmd.coSimulation.location))
		( let hasClocks = exists mv in seq fmd.modelVariables & is_Clock(mv) in
			hasClocks => fmd.coSimulation.hasEventMode = true );
----
// {vdm}
- See <<table-CoSimulation-details>>.


===== A.{section}.{subsection}.{counter:typerule} Rule: validOutput
[[validOutput]]
ifdef::hidden[]
// {vdm}

-- @DocLink("<FMI3_STANDARD>#ModelStructure")
// {vdm}
endif::[]
// {vdm}
----
validOutput: FmiModelDescription * ModelVariables +> bool
validOutput(fmd, evs) ==
	let outputRefs = { ev.valueReference | ev in seq evs & ev.causality = <output> },
		structRefs = { u.valueReference | u in seq
						default[seq of Output](fmd.modelStructure.output, []) }
	in
		if outputRefs <> {}
		then
			-- @OnFail("%NAME: Output variables but no outputs declared at %#s",
			-- loc2str(fmd.modelStructure.location))
			( structRefs <> {} )

			and
			-- @OnFail("%NAME: Outputs section does not match output variables at %#s",
			-- loc2str(fmd.modelStructure.location))
			( structRefs = outputRefs )
		else
			-- @OnFail("%NAME: Outputs should be omitted at %#s",
			-- loc2str(fmd.modelStructure.location))
			( structRefs = {} );
----
// {vdm}
See <<ModelStructure>>.

===== A.{section}.{subsection}.{counter:typerule} Rule: validStateDerivatives
[[validStateDerivatives]]
ifdef::hidden[]
// {vdm}

-- @DocLink("<FMI3_STANDARD>#ModelStructure")
// {vdm}
endif::[]
// {vdm}
----
validStateDerivatives: FmiModelDescription * ModelVariables +> bool
validStateDerivatives(fmd, evs) ==
	fmd.modelExchange <> nil => allOf
	([
		let ev = lookupVariable(uk.valueReference, evs) in
			-- @OnFail("%NAME: Derivative valueReference unknown at %#s", loc2str(uk.location))
			( ev <> nil )

			and allOf
			([
				-- @OnFail("%NAME: SV not a state derivative at %#s", loc2str(uk.location))
				( isStateDerivative(ev) ),

				-- @OnFail("%NAME: Derivative must be continuous at %#s", loc2str(uk.location))
				( uk.dependencies <> nil => ev.variability = <continuous> )
			])

		| uk in seq default[seq of ContinuousStateDerivative]
						(fmd.modelStructure.continuousStateDerivative, [])
	]);
----
// {vdm}
See <<ModelStructure>>.

===== A.{section}.{subsection}.{counter:typerule} Rule: validInitialUnknowns
[[validInitialUnknowns]]
ifdef::hidden[]
// {vdm}

-- @DocLink("<FMI3_STANDARD>#ModelStructure")
// {vdm}
endif::[]
// {vdm}
----
validInitialUnknowns: FmiModelDescription * ModelVariables +> bool
validInitialUnknowns(fmd, evs) ==
	let ctVars = continuousTimeStates(evs),
		sdVars = stateDerivatives(evs),
		required = { ev.valueReference | ev in seq evs &
			(ev.clocks = nil and ev.causality = <output>
				and not is_Clock(ev) and ev.initial in set { <approx>, <calculated> })

			or (ev.causality = <calculatedParameter>)

			or (ev in set ctVars
				and ev.initial in set { <approx>, <calculated> })

			or (ev in set sdVars
				and ev.initial in set { <approx>, <calculated> }) },

		optional = { ev.valueReference | ev in seq evs & ev.clocks <> nil },
		IUs      = default[seq of InitialUnknown](fmd.modelStructure.initialUnknown, []),
		allIUs   = { iu.valueReference | iu in seq IUs },
		allEIs   = { ei.valueReference | ei in seq
					default[seq of EventIndicator](fmd.modelStructure.eventIndicator, []) }
	in
		allOf
		([
			-- ?? @OnFail("%NAME: InitialUnknowns must include refs: %s",
			-- required \ allIUs) ?? This may not be true!
			-- ( required subset allIUs ),

			-- @OnFail("%NAME: InitialUnknowns can only include refs: %s",
			-- required union optional)
			( allIUs subset required union optional ),

			-- @OnFail("%NAME: InitialUnknowns cannot include EventIndicators: %s ",
			-- allIUs inter allEIs)
			( allIUs inter allEIs = {} ),

			-- @OnFail("%NAME: InitialUnknowns must not have duplicates: %s")
			( card allIUs = len IUs ),
			
			[
				iu.dependencies <> nil and iu.dependencies <> [] =>
					-- @OnFail("%NAME: InitialUnknown dependencies must all be known at %#s",
					-- loc2str(iu.location))
					( forall d in seq iu.dependencies & d not in set allIUs )

				| iu in seq IUs
			]
		]);
----
// {vdm}
See <<ModelStructure>>.

===== A.{section}.{subsection}.{counter:typerule} Rule: validEventIndicators
[[validEventIndicators]]
ifdef::hidden[]
// {vdm}

-- @DocLink("<FMI3_STANDARD>#ModelStructure")
// {vdm}
endif::[]
// {vdm}
----
validEventIndicators: FmiModelDescription * ModelVariables +> bool
validEventIndicators(fmd, evs) ==
	fmd.modelStructure.eventIndicator <> nil => allOf
	([
		-- @OnFail("%NAME: EventIndicator valueReference %s invalid at %#s",
		-- ei.valueReference, loc2str(ei.location))
		( exists ev in seq evs & ev.valueReference = ei.valueReference )

		| ei in seq fmd.modelStructure.eventIndicator
	]);
----
// {vdm}
See <<ModelStructure>>.

===== A.{section}.{subsection}.{counter:typerule} Rule: validOutputReferences
[[validOutputReferences]]
ifdef::hidden[]
// {vdm}

-- @DocLink("<FMI3_STANDARD>#ModelStructure")
// {vdm}
endif::[]
// {vdm}
----
validOutputReferences: FmiModelDescription * ModelVariables +> bool
validOutputReferences(fmd, evs) ==
	fmd.modelStructure.output <> nil => allOf
	([
		-- @OnFail("%NAME: Output valueReference %s is not an output at %#s",
		--  ei.valueReference, loc2str(ei.location))
		( exists ev in seq evs &
			ev.valueReference = ei.valueReference and ev.causality = <output> )

		| ei in seq fmd.modelStructure.output
	]);
----
// {vdm}
See <<ModelStructure>>.

===== A.{section}.{subsection}.{counter:typerule} Rule: validUnknownDependencies
[[validUnknownDependencies]]
ifdef::hidden[]
// {vdm}

-- @DocLink("<FMI3_STANDARD>#ModelStructure")
// {vdm}
endif::[]
// {vdm}
----
validUnknownDependencies: FmiModelDescription * ModelVariables +> bool
validUnknownDependencies(fmd, evs) ==
	let allUnknowns = conc
	[
		default[seq of Output](fmd.modelStructure.output, []),
		default[seq of ContinuousStateDerivative](fmd.modelStructure.continuousStateDerivative, []),
		default[seq of ClockedState](fmd.modelStructure.clockedState, []),
		default[seq of InitialUnknown](fmd.modelStructure.initialUnknown, []),
		default[seq of EventIndicator](fmd.modelStructure.eventIndicator, [])
	] in
		allUnknowns <> [] => allOf
		([
			unk.dependencies <> nil and unk.dependencies <> [] => allOf
			([
				-- @OnFail("%NAME: Unknown dependency %s invalid at %#s",
				-- vr, loc2str(unk.location))
				( exists ev in seq evs & ev.valueReference = vr )

				| vr in seq unk.dependencies
			])

			| unk in seq allUnknowns
		]);
----
// {vdm}
See <<ModelStructure>>.

===== A.{section}.{subsection}.{counter:typerule} Rule: validClockPriorities
[[validClockPriorities]]
ifdef::hidden[]
// {vdm}

-- @DocLink("<FMI3_STANDARD>#table-type-details")
// {vdm}
endif::[]
// {vdm}
----
validClockPriorities: FmiModelDescription * ModelVariables +> bool
validClockPriorities(fmd, evs) == allOf
	([
		if fmd.scheduledExecution <> nil then
			cases ev.causality:
				<input> ->
					-- @OnFail("%NAME: Input clock %s must have a priority for SE at %#s",
					-- ev.name, loc2str(ev.location))
					( ev.priority <> nil ),

				<output> ->	
					-- @OnFail("%NAME: Output clock %s must not have a priority for SE at %#s",
					-- ev.name, loc2str(ev.location))
					( ev.priority = nil ),

				<local> -> true,

				others	-> true
			end
		else
			-- @OnFail("%NAME: Clock %s must not have a priority unless SE at %#s",
			-- ev.name, loc2str(ev.location))
			( ev.priority = nil )
			
		| ev in seq evs & is_Clock(ev)
	]);
----
// {vdm}
See <<table-type-details>>.
